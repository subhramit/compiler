1. <program>: <otherFunctions>	<mainFunction>	
2. <mainFunction>: TK_MAIN	<stmts>	TK_END	
3. <otherFunctions>: <function>	<otherFunctions>	
4. <otherFunctions>: TK_EPS	
5. <function>: TK_FUNID	<input_par>	<output_par>	TK_SEM	<stmts>	TK_END	
6. <input_par>: TK_INPUT	TK_PARAMETER	TK_LIST	TK_SQL	<parameter_list>	TK_SQR	
7. <output_par>: TK_OUTPUT	TK_PARAMETER	TK_LIST	TK_SQL	<parameter_list>	TK_SQR	
8. <output_par>: TK_EPS	
9. <parameter_list>: <dataType>	TK_ID	<remaining_list>	
10. <dataType>: <primitiveDatatype>	
11. <dataType>: <constructedDatatype>	
12. <primitiveDatatype>: TK_INT	
13. <primitiveDatatype>: TK_REAL	
14. <constructedDatatype>: TK_RECORD	TK_RUID	
15. <constructedDatatype>: TK_UNION	TK_RUID	
16. <constructedDatatype>: TK_RUID	
17. <remaining_list>: TK_COMMA	<parameter_list>	
18. <remaining_list>: TK_EPS	
19. <stmts>: <typeDefinitions>	<declarations>	<otherStmts>	<returnStmt>	
20. <typeDefinitions>: <actualOrRedefined>	<typeDefinitions>	
21. <typeDefinitions>: TK_EPS	
22. <actualOrRedefined>: <typeDefinition>	
23. <actualOrRedefined>: <definetypestmt>	
24. <typeDefinition>: TK_RECORD	TK_RUID	<fieldDefinitions>	TK_ENDRECORD	
25. <typeDefinition>: TK_UNION	TK_RUID	<fieldDefinitions>	TK_ENDUNION	
26. <fieldDefinitions>: <fieldDefinition>	<fieldDefinition>	<moreFields>	
27. <fieldDefinition>: TK_TYPE	<fieldType>	TK_COLON	TK_FIELDID	TK_SEM	
28. <fieldType>: <primitiveDatatype>	
29. <fieldType>: <constructedDatatype>	
30. <moreFields>: <fieldDefinition>	<moreFields>	
31. <moreFields>: TK_EPS	
32. <declarations>: <declaration>	<declarations>	
33. <declarations>: TK_EPS	
34. <declaration>: TK_TYPE	<dataType>	TK_COLON	TK_ID	<global_or_not>	TK_SEM	
35. <global_or_not>: TK_COLON	TK_GLOBAL	
36. <global_or_not>: TK_EPS	
37. <otherStmts>: <stmt>	<otherStmts>	
38. <otherStmts>: TK_EPS	
39. <stmt>: <assignmentStmt>	
40. <stmt>: <iterativeStmt>	
41. <stmt>: <conditionalStmt>	
42. <stmt>: <ioStmt>	
43. <stmt>: <funCallStmt>	
44. <assignmentStmt>: <singleOrRecId>	TK_ASSIGNOP	<arithmeticExpression>	TK_SEM	
45. <singleOrRecId>: TK_ID	<option_single_constructed>	
46. <option_single_constructed>: TK_EPS	
47. <option_single_constructed>: <oneExpansion>	<moreExpansions>	
48. <oneExpansion>: TK_DOT	TK_FIELDID	
49. <moreExpansions>: <oneExpansion>	<moreExpansions>	
50. <moreExpansions>: TK_EPS	
51. <funCallStmt>: <outputParameters>	TK_CALL	TK_FUNID	TK_WITH	TK_PARAMETERS	<inputParameters>	TK_SEM	
52. <outputParameters>: TK_SQL	<idList>	TK_SQR	TK_ASSIGNOP	
53. <outputParameters>: TK_EPS	
54. <inputParameters>: TK_SQL	<idList>	TK_SQR	
55. <iterativeStmt>: TK_WHILE	TK_OP	<booleanExpression>	TK_CL	<stmt>	<otherStmts>	TK_ENDWHILE	
56. <conditionalStmt>: TK_IF	TK_OP	<booleanExpression>	TK_CL	TK_THEN	<stmt>	<otherStmts>	<elsePart>	
57. <elsePart>: TK_ELSE	<stmt>	<otherStmts>	TK_ENDIF	
58. <elsePart>: TK_ENDIF	
59. <ioStmt>: TK_READ	TK_OP	<var>	TK_CL	TK_SEM	
60. <ioStmt>: TK_WRITE	TK_OP	<var>	TK_CL	TK_SEM	
61. <arithmeticExpression>: <term>	<expPrime>	
62. <expPrime>: <lowPrecedenceOperators>	<term>	<expPrime>	
63. <expPrime>: TK_EPS	
64. <term>: <factor>	<termPrime>	
65. <termPrime>: <highPrecedenceOperators>	<factor>	<termPrime>	
66. <termPrime>: TK_EPS	
67. <factor>: TK_OP	<arithmeticExpression>	TK_CL	
68. <factor>: <var>	
69. <highPrecedenceOperators>: TK_MUL	
70. <highPrecedenceOperators>: TK_DIV	
71. <lowPrecedenceOperators>: TK_PLUS	
72. <lowPrecedenceOperators>: TK_MINUS	
73. <booleanExpression>: TK_OP	<booleanExpression>	TK_CL	<logicalOp>	TK_OP	<booleanExpression>	TK_CL	
74. <booleanExpression>: <var>	<relationalOp>	<var>	
75. <booleanExpression>: TK_NOT	TK_OP	<booleanExpression>	TK_CL	
76. <var>: <singleOrRecId>	
77. <var>: TK_NUM	
78. <var>: TK_RNUM	
79. <logicalOp>: TK_AND	
80. <logicalOp>: TK_OR	
81. <relationalOp>: TK_LT	
82. <relationalOp>: TK_LE	
83. <relationalOp>: TK_EQ	
84. <relationalOp>: TK_GT	
85. <relationalOp>: TK_GE	
86. <relationalOp>: TK_NE	
87. <returnStmt>: TK_RETURN	<optionalReturn>	TK_SEM	
88. <optionalReturn>: TK_SQL	<idList>	TK_SQR	
89. <optionalReturn>: TK_EPS	
90. <idList>: TK_ID	<more_ids>	
91. <more_ids>: TK_COMMA	<idList>	
92. <more_ids>: TK_EPS	
93. <definetypestmt>: TK_DEFINETYPE	<A>	TK_RUID	TK_AS	TK_RUID	
94. <A>: TK_RECORD	
95. <A>: TK_UNION	
